module Boss

$include Attributes
$include BossObject
$include CallCandidates
$include CmdExecute
$include CmdPreprocess
$include CmdStore
$include CmdType
$include Module
$include Organizer
$include Parser       [optional]
$include Procedure
$include Program
$include Resolver
$include Runtime
$include Scanner
$include Type
$include CollectTypes

uses Boss
uses Console/CommandLineParser

Boss( System.command_line_arguments )

class Boss
  PROPERTIES
    command  : Variant

    #staging_program   : Program
    #organized_program : Program
    #program           : Program

  METHODS
    method init
      noAction

    method init( args:String[] )
      command = _parse_args( args )

      # trace command
      # has //options and possibly //args

      if (command//options//help or command//args.count == 0)
        _print_usage
        System.exit 0
      endIf

      local error : Exception
      contingent
        forEach (arg in command//args)
          error = include( File(arg->String) )
          necessary (not error)
        endForEach

        error = update
        necessary (not error)

      unsatisfied
        #error.display
        Global.error.println error
        System.exit 1
      endContingent

    method execute( script:String )->Variant
      return Runtime.execute( script )

    method include( file:File )->BossError
      return Runtime.include( file )

    method load( file:File )->BossError
      return Runtime.load( file )

    method unload( file:File )
      Runtime.unload( file )

    method update->BossError
      return Runtime.update

    method _parse_args( args:String[] )->Variant
      # This method is unrelated to the Froley Parser
      local command = CommandLineParser().
      [
        option( "--flag",     &alias="-f" )
        option( "--help",     &aliases=["-h","-?"] )
        option( "--setting=", &alias="-s" )
      ].parse( args )
      return command

    method _print_usage
      println @|USAGE
               |  boss [OPTIONS] <filepath>
               |
               |OPTIONS
               |  --help, -h, -?
               |    Show this help text.
endClass
