module Boss

$include IO/Scanner
$include Codec/JSON

uses Geometry

class Variant [compound]
  DEFINITIONS
    # Internal use.
    TYPE_UNDEFINED =  0
    TYPE_BYTE      =  1
    TYPE_CHARACTER =  2
    TYPE_INT       =  3
    TYPE_REAL      =  4
    TYPE_LOGICAL   =  5
    TYPE_NULL      =  6
    TYPE_ANCHOR    =  7
    TYPE_BOX       =  8
    TYPE_CIRCLE    =  9
    TYPE_XY        = 10
    TYPE_XYZ       = 11
    TYPE_XYZW      = 12
    TYPE_OBJECT    = 13
    TYPE_STRING    = 14
    TYPE_LIST      = 15
    TYPE_TABLE     = 16

  NATIVE
    nativeHeader
      @|#define %NS%BOSS_VARIANT_TYPE_UNDEFINED  0
       |#define %NS%BOSS_VARIANT_TYPE_BYTE       1
       |#define %NS%BOSS_VARIANT_TYPE_CHARACTER  2
       |#define %NS%BOSS_VARIANT_TYPE_INT        3
       |#define %NS%BOSS_VARIANT_TYPE_REAL       4
       |#define %NS%BOSS_VARIANT_TYPE_LOGICAL    5
       |#define %NS%BOSS_VARIANT_TYPE_NULL       6
       |#define %NS%BOSS_VARIANT_TYPE_ANCHOR     7
       |#define %NS%BOSS_VARIANT_TYPE_BOX        8
       |#define %NS%BOSS_VARIANT_TYPE_CIRCLE     9
       |#define %NS%BOSS_VARIANT_TYPE_XY        10
       |#define %NS%BOSS_VARIANT_TYPE_XYZ       11
       |#define %NS%BOSS_VARIANT_TYPE_XYZW      12
       |#define %NS%BOSS_VARIANT_TYPE_OBJECT    13
       |#define %NS%BOSS_VARIANT_TYPE_STRING    14
       |#define %NS%BOSS_VARIANT_TYPE_LIST      15
       |#define %NS%BOSS_VARIANT_TYPE_TABLE     16

  PROPERTIES
    # All properties should be considered INTERNAL USE ONLY because they are
    # tricky - .type must be checked to know which property is valid.
    type      : Int
    object    : Object # Can't be mixed into the union due to GC constraints

    box       : Box       [faux]
    byte      : Byte      [faux]
    character : Character [faux]
    circle    : Circle    [faux]
    integer   : Int       [faux]
    logical   : Logical   [faux]
    real      : Real      [faux]
    xy        : XY        [faux]
    xyz       : XYZ       [faux]
    xyzw      : XYZW      [faux]

    native @|union
            |{
            |  %Ns%GeometryBox    box;
            |  %Ns%RogueByte      byte;
            |  %Ns%RogueCharacter character;
            |  %Ns%GeometryCircle circle;
            |  %Ns%RogueInt       integer;
            |  %Ns%RogueLogical   logical;
            |  %Ns%RogueReal      real;
            |  %Ns%GeometryXY     xy;
            |  %Ns%GeometryXYZ    xyz;
            |  %Ns%GeometryXYZW   xyzw;
            |};

  GLOBAL METHODS
    method create->Boss::Variant
      return native( "(%Ns%BossVariant) { %NS%BOSS_VARIANT_TYPE_UNDEFINED }" )->Boss::Variant  # undefined

    method create( value:Logical )->Boss::Variant
      return native( "(%Ns%BossVariant) {  %NS%BOSS_VARIANT_TYPE_LOGICAL, 0, {.logical=$value} }" )->Boss::Variant

    method create( value:Byte )->Boss::Variant
      return native( "(%Ns%BossVariant) {  %NS%BOSS_VARIANT_TYPE_BYTE, 0, {.byte=$value} }" )->Boss::Variant

    method create( value:Character )->Boss::Variant
      return native( "(%Ns%BossVariant) {  %NS%BOSS_VARIANT_TYPE_CHARACTER, 0, {.character=$value} }" )->Boss::Variant

    method create( value:Int )->Boss::Variant
      return native( "(%Ns%BossVariant) {  %NS%BOSS_VARIANT_TYPE_INT, 0, {.integer=$value} }" )->Boss::Variant

    method create( value:Real )->Boss::Variant
      return native( "(%Ns%BossVariant) {  %NS%BOSS_VARIANT_TYPE_REAL, 0, {.real=$value} }" )->Boss::Variant

    method create( value:XY )->Boss::Variant
      return native( "(%Ns%BossVariant) {  %NS%BOSS_VARIANT_TYPE_XY, 0, {.xy=$value} }" )->Boss::Variant

    method create( value:XYZ )->Boss::Variant
      return native( "(%Ns%BossVariant) {  %NS%BOSS_VARIANT_TYPE_XYZ, 0, {.xyz=$value} }" )->Boss::Variant

    method create( value:XYZW )->Boss::Variant
      return native( "(%Ns%BossVariant) {  %NS%BOSS_VARIANT_TYPE_XYZW, 0, {.xyzw=$value} }" )->Boss::Variant

    method create( value:Anchor )->Boss::Variant
      return native( "(%Ns%BossVariant) { %NS%BOSS_VARIANT_TYPE_ANCHOR, 0, {.xy=$value.position} }" )->Boss::Variant

    method create( value:Box )->Boss::Variant
      return native( "(%Ns%BossVariant) { %NS%BOSS_VARIANT_TYPE_BOX, 0, {.box=$value} }" )->Boss::Variant

    method create( value:Circle )->Boss::Variant
      return native( "(%Ns%BossVariant) { %NS%BOSS_VARIANT_TYPE_CIRCLE, 0, {.circle=$value} }" )->Boss::Variant

    method create( value:Object )->Boss::Variant [preferred]
      if (value) return native( "(%Ns%BossVariant) { %NS%BOSS_VARIANT_TYPE_OBJECT, $value }" )->Boss::Variant
      else       return native( "(%Ns%BossVariant) { %NS%BOSS_VARIANT_TYPE_NULL }" )->Boss::Variant  # null value

    method create( value:String )->Boss::Variant
      if (value) return native( "(%Ns%BossVariant) { %NS%BOSS_VARIANT_TYPE_STRING, (%Ns%RogueObject*)$value }" )->Boss::Variant
      else       return native( "(%Ns%BossVariant) { %NS%BOSS_VARIANT_TYPE_NULL }" )->Boss::Variant  # null value

    method create( value:Boss::Variant[] )->Boss::Variant
      if (value) return native( "(%Ns%BossVariant) { %NS%BOSS_VARIANT_TYPE_LIST, (%Ns%RogueObject*)$value }" )->Boss::Variant
      else       return native( "(%Ns%BossVariant) { %NS%BOSS_VARIANT_TYPE_NULL }" )->Boss::Variant  # null value

    method create( value:[Boss::Variant:Boss::Variant] )->Boss::Variant
      if (value) return native( "(%Ns%BossVariant) { %NS%BOSS_VARIANT_TYPE_TABLE, (%Ns%RogueObject*)$value }" )->Boss::Variant
      else       return native( "(%Ns%BossVariant) { %NS%BOSS_VARIANT_TYPE_NULL }" )->Boss::Variant  # null value

    method create_list->Boss::Variant
      # Convenience syntax: @[] is equivalent to Boss::Variant.create_list()
      local data = Boss::Variant[]
      return native( "(%Ns%BossVariant) { %NS%BOSS_VARIANT_TYPE_LIST, (%Ns%RogueObject*)$data }" )->Boss::Variant

    method create_table->Boss::Variant
      # Convenience syntax: @{} is equivalent to Boss::Variant.create_table()
      local data = [Boss::Variant:Boss::Variant]
      return native( "(%Ns%BossVariant) { %NS%BOSS_VARIANT_TYPE_TABLE, (%Ns%RogueObject*)$data }" )->Boss::Variant

  METHODS
    method add( value:Boss::Variant )
      which (type)
        case TYPE_LIST
          object->(as Boss::Variant[]).add( value )
      endWhich

    method add_all( value:Boss::Variant )
      which (type)
        case TYPE_LIST
          local list = object->(as Boss::Variant[])
          list.reserve( value.count )
          list.add( forEach in value )
      endWhich

    method as_list->Boss::Variant[]
      # If this value .is_list, returns the backing Boss::Variant[] list; otherwise returns null.
      if (type != TYPE_LIST) return null
      return object->(as Boss::Variant[])

    method as_string->String
      # If this value .is_string, returns the backing String; otherwise returns null.
      #
      #   Boss::Variant     | ->String  |  .as_string
      #   ----------|-----------|------------
      #   null      | "null"    | null
      #   undefined | ""        | null
      #   "ABC"     | "ABC"     | ABC
      #   [3,4]     | "[3,4]"   | null
      #   {x:22}    | "{x:22}"  | null
      if (type != TYPE_STRING) return null
      return object->(as String)

    method as_table->[Boss::Variant:Boss::Variant]
      # If this value .is_table, returns the backing [Boss::Variant:Boss::Variant] table; otherwise returns null.
      if (type != TYPE_TABLE) return null
      return object->(as [Boss::Variant:Boss::Variant])

    method at( index:Int )->Boss::Variant
      which (type)
        case TYPE_LIST
          local list = object->(as Boss::Variant[])
          if (index < 0 or index >= list.count) return undefined
          return list[index]

        case TYPE_STRING
          local st = object->(as String)
          if (index < 0 or index >= st.count) return undefined
          return st[index]

        case TYPE_TABLE
          local table = object->(as [Boss::Variant:Boss::Variant])
          if (index < 0 or index >= table.count) return undefined
          return table.at(index)

        others
          if (index == 0) return this
          return undefined
      endWhich

    method clear
      which (type)
        case TYPE_LIST
          object->(as Boss::Variant[]).clear
        case TYPE_TABLE
          object->(as [Boss::Variant:Boss::Variant]).clear
      endWhich

    method contains( value:Boss::Variant )->Logical
      which (type)
        case TYPE_STRING
          which (value.type)
            case TYPE_CHARACTER: return object->(as String).contains( value.character )
            case TYPE_STRING:    return object->(as String).contains( value.object->(as String) )
            others:              return false
          endWhich
        case TYPE_LIST
          return object->(as Boss::Variant[]).contains( value )
        case TYPE_TABLE
          return object->(as [Boss::Variant:Boss::Variant]).contains( value->String )
        others
          return false
      endWhich

    method count->Int
      which (type)
        case TYPE_STRING
          return object->(as String).count
        case TYPE_LIST
          return object->(as Boss::Variant[]).count
        case TYPE_TABLE
          return object->(as [Boss::Variant:Boss::Variant]).count
        case TYPE_UNDEFINED
          return 0
        others
          return 1
      endWhich

    method entries->TableEntriesIterator<<Boss::Variant,Boss::Variant>>
      if (type != TYPE_TABLE) return TableEntriesIterator<<Boss::Variant,Boss::Variant>>( null )
      return object->(as [Boss::Variant:Boss::Variant]).entries

    method ensure_list( name:String )->Boss::Variant
      if (not is_table) return @[]

      local list = this[name]
      if (list) return list

      list = @[]
      this[name] = list
      return list

    method ensure_table( name:String )->Boss::Variant
      if (not is_table) return @{}

      local table = this[name]
      if (table) return table

      table = @{}
      this[name] = table
      return table

    method first->Boss::Variant
      which (type)
        case TYPE_LIST
          local list = object->(as Boss::Variant[])
          if (list.count == 0) return undefined
          return list[0]

        case TYPE_STRING
          local st = object->(as String)
          if (st.count == 0) return undefined
          return st[0]

        case TYPE_TABLE
          local table = object->(as [Boss::Variant:Boss::Variant])
          if (table.count == 0) return undefined
          return table.at(0)

        others
          return this
      endWhich

    method first( query:Function(Boss::Variant)->Logical )->Boss::Variant
      forEach (value in this)
        if (query(value)) return value
      endForEach
      return undefined

    method get( index:Int )->Boss::Variant
      return get( Boss::Variant(index) )

    method get( key:String )->Boss::Variant
      return get( Boss::Variant(key) )

    method get( key:Boss::Variant )->Boss::Variant [preferred]
      which (type)
        case TYPE_TABLE
          local table = object->(as [Boss::Variant:Boss::Variant])
          local entry = table.find( key )
          if (entry) return entry.value
          else       return undefined

        others
          if (key.is_integer) return at( key->Int )
          else                return undefined
      endWhich

    method hash_code->Int
      which (type)
        case TYPE_UNDEFINED: return 0
        case TYPE_NULL:      return 0
        case TYPE_LOGICAL:   return which{ logical:1 || 0 }
        case TYPE_BYTE:      return byte.hash_code
        case TYPE_CHARACTER: return character.hash_code
        case TYPE_INT:       return integer.hash_code
        case TYPE_REAL:      return real.hash_code
        case TYPE_XY:        return xy.hash_code
        case TYPE_XYZ:       return xyz.hash_code
        case TYPE_XYZW:      return xyzw.hash_code
        case TYPE_ANCHOR:    return xy.hash_code
        case TYPE_BOX:       return box.hash_code
        case TYPE_CIRCLE:    return circle.hash_code
        case TYPE_OBJECT:    return object.hash_code
        case TYPE_STRING:    return object->(as String).hash_code
        case TYPE_LIST:      return object->(as Boss::Variant[]).hash_code
        case TYPE_TABLE:     return object->(as [Boss::Variant:Boss::Variant]).hash_code
        others:
          Global.error.print( "[Boss::Variant.hash_code()] Undefined type $"(type) )
          return 0
      endWhich

    method insert( value:Boss::Variant, before_index=0:Int )
      which (type)
        case TYPE_LIST
          object->(as Boss::Variant[]).insert( value, before_index )
      endWhich

    method is_collection->Logical
      which (type)
        case TYPE_STRING, TYPE_LIST, TYPE_TABLE
          return true
        others
          return false
      endWhich

    method is_anchor->Logical
      return (type == TYPE_ANCHOR)

    method is_box->Logical
      return (type == TYPE_BOX)

    method is_byte->Logical
      return (type == TYPE_BYTE)

    method is_character->Logical
      return (type == TYPE_CHARACTER)

    method is_circle->Logical
      return (type == TYPE_CIRCLE)

    method is_empty->Logical
      return (count == 0)

    method is_geometry->Logical
      return (type >= TYPE_ANCHOR and type <= TYPE_XYZW)

    method is_int->Logical
      return (type == TYPE_INT)

    method is_integer->Logical
      return (type >= TYPE_BYTE and type <= TYPE_INT)

    method is_logical->Logical
      return (type == TYPE_LOGICAL)

    method is_list->Logical
      return (type == TYPE_LIST)

    method is_null->Logical
      return (type == TYPE_NULL)

    method is_number->Logical
      return (type >= TYPE_BYTE and type <= TYPE_REAL)

    method is_object->Logical
      return (type == TYPE_OBJECT)

    method is_real->Logical
      return (type == TYPE_REAL)

    method is_string->Logical
      return (type == TYPE_STRING)

    method is_table->Logical
      return (type == TYPE_TABLE)

    method is_undefined->Logical
      return (type == TYPE_UNDEFINED)

    method is_xy->Logical
      return (type == TYPE_XY)

    method is_xyz->Logical
      return (type == TYPE_XYZ)

    method is_xyzw->Logical
      return (type == TYPE_XYZW)

    method keys->TableKeysIterator<<Boss::Variant,Boss::Variant>>
      if (type != TYPE_TABLE) return TableKeysIterator<<Boss::Variant,Boss::Variant>>( null )
      return object->(as [Boss::Variant:Boss::Variant]).keys

    method last->Boss::Variant
      which (type)
        case TYPE_LIST
          local list = object->(as Boss::Variant[])
          if (list.count == 0) return undefined
          return list[list.count-1]

        case TYPE_STRING
          local st = object->(as String)
          if (st.count == 0) return undefined
          return st[st.count-1]

        case TYPE_TABLE
          local table = object->(as [Boss::Variant:Boss::Variant])
          if (table.count == 0) return undefined
          return table.at( table.count-1 )

        others
          return this
      endWhich

    method last( query:Function(Boss::Variant)->Logical )->Boss::Variant
      forEach (value in this step -1)
        if (query(value)) return value
      endForEach
      return undefined

    method locate( value:Boss::Variant, i1=null:Int? )->Int?
      which (type)
        case TYPE_STRING
          which (value.type)
            case TYPE_CHARACTER: return object->(as String).locate( value.character, i1 )
            case TYPE_STRING:    return object->(as String).locate( value.object->(as String), i1 )
            others:              return null
          endWhich
        case TYPE_LIST
          return object->(as Boss::Variant[]).locate( value, i1 )
        others
          return null
      endWhich

    method locate_last( value:Boss::Variant, i1=null:Int? )->Int?
      which (type)
        case TYPE_STRING
          which (value.type)
            case TYPE_CHARACTER: return object->(as String).locate_last( value.character, i1 )
            case TYPE_STRING:    return object->(as String).locate_last( value.object->(as String), i1 )
            others:              return null
          endWhich
        case TYPE_LIST
          return object->(as Boss::Variant[]).locate_last( value, i1 )
        others
          return null
      endWhich

    method operator?->Logical
      if (type == TYPE_NULL or type == TYPE_UNDEFINED) return false
      return this->Logical

    method operator==(other:Boss::Variant)->Int
      return (this <> other) == 0

    method operator<>(other:Boss::Variant)->Int
      if (type == TYPE_UNDEFINED) return (other.type != TYPE_UNDEFINED)

      if (type <= TYPE_REAL and other.type <= TYPE_REAL)
        # Both are primitives
        if (type == TYPE_REAL or other.type == TYPE_REAL) return (real <> other.real).sign
        return (integer <> other.integer).sign
      endIf

      if (type != other.type) return type <> other.type

      which (type)
        case TYPE_LOGICAL
          return logical <> other.logical
        case TYPE_NULL
          return 0  # equal
        case TYPE_XY, TYPE_ANCHOR
          return xy <> other.xy
        case TYPE_XYZ
          return xyz <> other.xyz
        case TYPE_XYZW
          return xyzw <> other.xyzw
        case TYPE_BOX
          if (box.size == other.box.size) return box.position <> other.box.position
          else                            return box.size <> other.box.size
        case TYPE_CIRCLE
          return circle <> other.circle
        case TYPE_OBJECT
          if (object == other.object) return 0
          return object.object_id <> other.object.object_id
        case TYPE_STRING
          return object->(as String) <> other.object->(as String)
        case TYPE_LIST
          if (object->(as Boss::Variant[]) == other.object->(as Boss::Variant[])) return 0
          return object.object_id <> other.object.object_id
        case TYPE_TABLE
          if (object->(as [Boss::Variant:Boss::Variant]) == other.object->(as [Boss::Variant:Boss::Variant])) return 0
          return object.object_id <> other.object.object_id
        others
          return type <> other.type
      endWhich

    method operator-()->Boss::Variant
      which (type)
        case TYPE_BYTE:      return Boss::Variant( -byte )
        case TYPE_INT:       return Boss::Variant( -integer )
        case TYPE_REAL:      return Boss::Variant( -real )
        case TYPE_LOGICAL:   return Boss::Variant( -logical )
        case TYPE_XY:        return Boss::Variant( -xy )
        case TYPE_XYZ:       return Boss::Variant( -xyz )
        case TYPE_XYZW:      return Boss::Variant( -xyzw )
        others
          return undefined
      endWhich

    method operator!()->Boss::Variant
      if (this.is_number)
        return Boss::Variant( !(this->Int) )
      else
        return undefined
      endIf

    method operator+( other:Boss::Variant )->Boss::Variant
      local (a,b) = to_compatible_values( other )
      which (a.type)
        case TYPE_BYTE:      return Boss::Variant( a.byte      + b.byte )
        case TYPE_CHARACTER: return Boss::Variant( a.character + b.character )
        case TYPE_INT:       return Boss::Variant( a.integer   + b.integer )
        case TYPE_REAL:      return Boss::Variant( a.real      + b.real )
        case TYPE_LOGICAL:   return Boss::Variant( a.logical or b.logical )
        case TYPE_XY:        return Boss::Variant( a.xy        + b.xy )
        case TYPE_XYZ:       return Boss::Variant( a.xyz       + b.xyz )
        case TYPE_XYZW:      return Boss::Variant( a.xyzw      + b.xyzw )
        case TYPE_STRING:    return Boss::Variant( a->String   + b->String )
        others
          return undefined
      endWhich

    method operator-( other:Boss::Variant )->Boss::Variant
      local (a,b) = to_compatible_values( other )
      which (a.type)
        case TYPE_BYTE:      return Boss::Variant( a.byte      - b.byte )
        case TYPE_CHARACTER: return Boss::Variant( a.character - b.character )
        case TYPE_INT:       return Boss::Variant( a.integer   - b.integer )
        case TYPE_REAL:      return Boss::Variant( a.real      - b.real )
        case TYPE_XY:        return Boss::Variant( a.xy        - b.xy )
        case TYPE_XYZ:       return Boss::Variant( a.xyz       - b.xyz )
        case TYPE_XYZW:      return Boss::Variant( a.xyzw      - b.xyzw )
        others
          return undefined
      endWhich

    method operator*( other:Boss::Variant )->Boss::Variant
      if (this.type == TYPE_STRING and other.is_number) return Boss::Variant( this->String * other->Int )
      if (this.is_number and other.type == TYPE_STRING) return Boss::Variant( other->String * this->Int )

      local (a,b) = to_compatible_values( other )
      which (a.type)
        case TYPE_BYTE:      return Boss::Variant( a.byte      * b.byte )
        case TYPE_CHARACTER: return Boss::Variant( a.character * b.character )
        case TYPE_INT:       return Boss::Variant( a.integer   * b.integer )
        case TYPE_REAL:      return Boss::Variant( a.real      * b.real )
        case TYPE_LOGICAL:   return Boss::Variant( a.logical and b.logical )
        case TYPE_XY:        return Boss::Variant( a.xy        * b.xy )
        case TYPE_XYZ:       return Boss::Variant( a.xyz       * b.xyz )
        case TYPE_XYZW:      return Boss::Variant( a.xyzw      * b.xyzw )
        others
          return undefined
      endWhich

    method operator/( other:Boss::Variant )->Boss::Variant
      if (type == TYPE_STRING and other.type == TYPE_STRING) return Boss::Variant( this->String / other->String )

      local (a,b) = to_compatible_values( other )
      which (a.type)
        case TYPE_BYTE:      return Boss::Variant( a.byte      / b.byte )
        case TYPE_CHARACTER: return Boss::Variant( a.character / b.character )
        case TYPE_INT:       return Boss::Variant( a.integer   / b.integer )
        case TYPE_REAL:      return Boss::Variant( a.real      / b.real )
        case TYPE_XY:        return Boss::Variant( a.xy        / b.xy )
        case TYPE_XYZ:       return Boss::Variant( a.xyz       / b.xyz )
        case TYPE_XYZW:      return Boss::Variant( a.xyzw      / b.xyzw )
        others
          return undefined
      endWhich

    method operator%( other:Boss::Variant )->Boss::Variant
      local (a,b) = to_compatible_values( other )
      which (a.type)
        case TYPE_BYTE:      return Boss::Variant( a.byte      % b.byte )
        case TYPE_CHARACTER: return Boss::Variant( a.character % b.character )
        case TYPE_INT:       return Boss::Variant( a.integer   % b.integer )
        case TYPE_REAL:      return Boss::Variant( a.real      % b.real )
        case TYPE_XY:        return Boss::Variant( a.xy        % b.xy )
        case TYPE_XYZ:       return Boss::Variant( a.xyz       % b.xyz )
        case TYPE_XYZW:      return Boss::Variant( a.xyzw      % b.xyzw )
        others
          return undefined
      endWhich

    method operator^( other:Boss::Variant )->Boss::Variant
      local (a,b) = to_compatible_values( other )
      which (a.type)
        case TYPE_BYTE:      return Boss::Variant( a.byte      ^ b.byte )
        case TYPE_CHARACTER: return Boss::Variant( a.character ^ b.character )
        case TYPE_INT:       return Boss::Variant( a.integer   ^ b.integer )
        case TYPE_REAL:      return Boss::Variant( a.real      ^ b.real )
        case TYPE_XY:        return Boss::Variant( a.xy        ^ b.xy )
        case TYPE_XYZ:       return Boss::Variant( a.xyz       ^ b.xyz )
        case TYPE_XYZW:      return Boss::Variant( a.xyzw      ^ b.xyzw )
        others
          return undefined
      endWhich

    method operator&( other:Boss::Variant )->Boss::Variant
      if (this.is_number and other.is_number)
        return Boss::Variant( this->Int & other->Int )
      else
        return undefined
      endIf

    method operator|( other:Boss::Variant )->Boss::Variant
      if (this.is_number and other.is_number)
        return Boss::Variant( this->Int | other->Int )
      else
        return undefined
      endIf

    method operator~( other:Boss::Variant )->Boss::Variant
      if (this.is_number and other.is_number)
        return Boss::Variant( this->Int ~ other->Int )
      else
        return undefined
      endIf

    method operator:<<:( other:Boss::Variant )->Boss::Variant
      if (this.is_number and other.is_number)
        return Boss::Variant( this->Int :<<: other->Int )
      else
        return undefined
      endIf

    method operator:>>:( other:Boss::Variant )->Boss::Variant
      if (this.is_number and other.is_number)
        return Boss::Variant( this->Int :>>: other->Int )
      else
        return undefined
      endIf

    method operator:>>>:( other:Boss::Variant )->Boss::Variant
      if (this.is_number and other.is_number)
        return Boss::Variant( this->Int :>>>: other->Int )
      else
        return undefined
      endIf

    method print_to( buffer:PrintWriter )
      which (type)
        case TYPE_UNDEFINED: return
        case TYPE_NULL:      buffer.print( "null" )
        case TYPE_LOGICAL:   buffer.print( which{logical:"true" || "false"} )
        case TYPE_BYTE:      byte.print_to( buffer )
        case TYPE_CHARACTER: character.print_to( buffer )
        case TYPE_INT:       integer.print_to( buffer )
        case TYPE_REAL:      real.print_to( buffer )
        case TYPE_XY:        xy.print_to( buffer )
        case TYPE_XYZ:       xyz.print_to( buffer )
        case TYPE_XYZW:      xyzw.print_to( buffer )
        case TYPE_ANCHOR:    Anchor(xy).print_to( buffer )
        case TYPE_BOX:       box.print_to( buffer )
        case TYPE_CIRCLE:    circle.print_to( buffer )
        case TYPE_OBJECT:    object.print_to( buffer )
        case TYPE_STRING:    buffer.print( object->(as String) )
        case TYPE_LIST:      object->(as Boss::Variant[]).print_to( buffer )
        case TYPE_TABLE:     object->(as [Boss::Variant:Boss::Variant]).print_to( buffer )
        others
          Global.error.print( "[Boss::Variant.print_to()] Undefined type $"(type) )
          buffer.print "undefined"
      endWhich

    method remove( value:Boss::Variant )->Boss::Variant
      which (type)
        case TYPE_LIST
          return object->(as Boss::Variant[]).remove( value )
        case TYPE_TABLE
          return object->(as [Boss::Variant:Boss::Variant]).remove( value )
        others
          return undefined
      endWhich

    method remove_at( index:Int )->Boss::Variant
      which (type)
        case TYPE_LIST
          return object->(as Boss::Variant[]).remove_at( index )
        case TYPE_TABLE
          return object->(as [Boss::Variant:Boss::Variant]).remove_at( index )
        others
          return undefined
      endWhich

    method remove_first->Boss::Variant
      if (count == 0) return undefined
      which (type)
        case TYPE_LIST
          return object->(as Boss::Variant[]).remove_at( 0 )
        case TYPE_TABLE
          return object->(as [Boss::Variant:Boss::Variant]).remove_at( 0 )
        others
          return undefined
      endWhich

    method remove_last->Boss::Variant
      local n = count
      if (n == 0) return undefined
      which (type)
        case TYPE_LIST
          return object->(as Boss::Variant[]).remove_at( n-1 )
        case TYPE_TABLE
          return object->(as [Boss::Variant:Boss::Variant]).remove_at( n-1 )
        others
          return undefined
      endWhich

    method set( index:Int, value:Boss::Variant )
      which (type)
        case TYPE_LIST
          local list = object->(as Boss::Variant[])
          if (index < 0 or index >= list.count) return
          list[index] = value
        case TYPE_TABLE
          set( Boss::Variant(index), value )
      endWhich

    method set( key:String, value:Boss::Variant ) [preferred]
      set( Boss::Variant(key), value )

    method set( key:Boss::Variant, value:Boss::Variant )
      if (key.is_number)
        set( key->Int, value )
      else
        which (type)
          case TYPE_TABLE
            object->(as [Boss::Variant:Boss::Variant])[ key ] = value
        endWhich
      endIf

    method sort( compare_fn:(Function(Boss::Variant,Boss::Variant)->Logical) )
      # 'compare_fn' should return true if the first parameter should be positioned before the second parameter.
      # The values passed will be keys for a Boss::Variant Table or elements for a Boss::Variant List.
      which (type)
        case TYPE_LIST
          local list = object->(as Boss::Variant[])
          list.sort( compare_fn )
        case TYPE_TABLE
          local table = object->(as [Boss::Variant:Boss::Variant])
          table.sort( (a,b) => compare_fn(a.key,b.key) )
      endWhich

    method to->Anchor
      which (type)
        case TYPE_ANCHOR, TYPE_XY: return Anchor( xy )
        others:                    return Anchor( this->XY )
      endWhich

    method to->Box
      which (type)
        case TYPE_BOX:    return box
        case TYPE_CIRCLE: return Box( box.position-box.radius, Vector2(box.radius*2) )
        case TYPE_XY:     return Box(xy)
        others:           return $defaultValue<<Box>>
      endWhich

    method to->Byte
      which (type)
        case TYPE_BYTE:      return byte
        case TYPE_CHARACTER: return character->Byte
        case TYPE_INT:       return integer->Byte
        case TYPE_REAL:      return real->Byte
        case TYPE_OBJECT:    return (object->String->Int)?
        case TYPE_STRING:    return (object->String->Int)?
        others:              return 0
      endWhich

    method to->Character
      which (type)
        case TYPE_BYTE:      return byte->Character
        case TYPE_CHARACTER: return character
        case TYPE_INT:       return integer->Character
        case TYPE_REAL:      return real->Character
        case TYPE_OBJECT, TYPE_STRING
          local st = object->String
          if (st and st.count) return st[0]
          else                 return 0
        others:              return 0
      endWhich

    method to->Circle
      which (type)
        case TYPE_BOX:    return Circle( box.position + box.size/2, box.size/2 )
        case TYPE_CIRCLE: return circle
        others:           return $defaultValue<<Circle>>
      endWhich

    method to->Logical
      which (type)
        case TYPE_LOGICAL:   return logical
        case TYPE_BYTE:      return byte?
        case TYPE_CHARACTER: return character?
        case TYPE_INT:       return integer?
        case TYPE_REAL:      return real?
        case TYPE_XY, TYPE_ANCHOR: return xy.x? or xy.y?
        case TYPE_XYZ:       return xyz.x? or xyz.y? or xyz.z?
        case TYPE_XYZW:      return xyzw.x? or xyzw.y? or xyzw.z? or xyzw.w?
        case TYPE_BOX:       return box.size.x? or box.size.y?
        case TYPE_CIRCLE:    return circle.radius?
        case TYPE_OBJECT:    return true
        case TYPE_STRING:    return true
        case TYPE_LIST:      return true
        case TYPE_TABLE:     return true
        others:              return false
      endWhich

    method to->Object
      which (type)
        case TYPE_OBJECT, TYPE_STRING: return object
        others:                        return null
      endWhich

    method to->String
      local result = String()
      print_to( result )
      return result

    method to->XY
      which (type)
        case TYPE_XY, TYPE_ANCHOR: return xy
        case TYPE_XYZ:             return xyz->XY
        case TYPE_XYZW:            return xyzw->XY
        case TYPE_INT:             return XY(integer,integer)
        case TYPE_REAL:            return XY(real,real)
        others:                    return $defaultValue<<XY>>
      endWhich

    method to->XYZ
      which (type)
        case TYPE_XY:              return XYZ(xy)
        case TYPE_XYZ:             return xyz
        case TYPE_XYZW:            return xyzw->XYZ
        case TYPE_INT:             return XYZ(integer,integer,integer)
        case TYPE_REAL:            return XYZ(real,real,real)
        others:                    return $defaultValue<<XYZ>>
      endWhich

    method to->XYZW
      which (type)
        case TYPE_XY:              return XYZW(xy)
        case TYPE_XYZ:             return XYZW(xyz)
        case TYPE_XYZW:            return xyzw
        case TYPE_INT:             return XYZW(integer,integer,integer)
        case TYPE_REAL:            return XYZW(real,real,real)
        others:                    return $defaultValue<<XYZW>>
      endWhich

    method to<<$Type>>->$Type
      if ($isReference<<$Type>>)
        return this->Object->(as $Type)
      elseIf ($isEnum<<$Type>>)
        local st = this->String
        if (st) return $Type(st)
        else    return $Type(0)
      elseIf ($Type instanceOf OptionalType)
        if (type == TYPE_NULL or type == TYPE_UNDEFINED) return null
        if ($exists<<$Type(this)>>)
          return $Type(this)
        elseIf (type == TYPE_OBJECT)
          local box = this->Object->(as Boxed<<$Type>>)
          if (box) return box->$Type
          return $defaultValue<<$Type>>
        else
          return $defaultValue<<$Type>>
        endIf
      else
        local box = this->Object->(as Boxed<<$Type>>)
        if (box) return box->$Type
        return $defaultValue<<$Type>>
      endIf

    method to_optional<<$Type>>->$Type?
      if (type == TYPE_NULL or type == TYPE_UNDEFINED) return null
      return to<<$Type>>

    method to_compatible_values( other:Boss::Variant )->(Boss::Variant,Boss::Variant)
      if (type == other.type) return (this,other)
      if (type == TYPE_STRING or other.type == TYPE_STRING) return (Boss::Variant(this->String),Boss::Variant(other->String))
      if (type == TYPE_UNDEFINED or other.type == TYPE_UNDEFINED) return (undefined,undefined)

      if (type <= TYPE_REAL and other.type <= TYPE_REAL)
        if (type == TYPE_REAL   or other.type == TYPE_REAL)   return (Boss::Variant(this->Real),  Boss::Variant(other->Real))
        if (type == TYPE_INT   or other.type == TYPE_INT)     return (Boss::Variant(this->Int),   Boss::Variant(other->Int))
        if (type == TYPE_CHARACTER or other.type == TYPE_CHARACTER) return (Boss::Variant(this->Character),Boss::Variant(other->Character))
        return (Boss::Variant(this->Byte),Boss::Variant(other->Byte))
      endIf

      if (type >= TYPE_XY and type <= TYPE_XYZW and other.type >= TYPE_XY and other.type <= TYPE_XYZW)
        if (type == TYPE_XYZW or other.type == TYPE_XYZW) return (this->XYZW,other->XYZW)
        if (type == TYPE_XYZ  or other.type == TYPE_XYZ)  return (this->XYZ, other->XYZ)
        return (this->XY, other->XY)
      endIf


      return (undefined,undefined)

    method to_json( &formatted, &omit_commas )->String
      local result = String()
      write_json( result, &=formatted, &=omit_commas )
      return result

    method to_list<<$DataType>>->$DataType[]
      local result = $DataType[]( count )
      forEach (value in this)
        result.add( value->$DataType )
      endForEach
      return result

    method values->TableValuesIterator<<Boss::Variant,Boss::Variant>>
      if (type != TYPE_TABLE) return TableValuesIterator<<Boss::Variant,Boss::Variant>>( null )
      return object->(as [Boss::Variant:Boss::Variant]).values

    method write_json( builder:String, &formatted, &omit_commas )
      which (type)
        case TYPE_UNDEFINED
          noAction
        case TYPE_BYTE
          builder.print( byte )
        case TYPE_CHARACTER
          # Write single character as a string
          builder.print '"'
          write_json_character( character, builder )
          builder.print '"'
        case TYPE_INT
          builder.print( integer )
        case TYPE_REAL
          builder.print( real )
        case TYPE_LOGICAL
          builder.print( logical )
        case TYPE_NULL
          builder.print( "null" )
        case TYPE_XY:
          builder.[ print('['), print(xy.x), print(','), print(xy.y), print(']') ]
        case TYPE_XYZ
          builder.[ print('['), print(xyz.x), print(','), print(xyz.y), print(','), print(xyz.z), print(']') ]
        case TYPE_XYZW
          builder.[ print('['), print(xyzw.x), print(','), print(xyzw.y), print(','), print(xyzw.z) ]
          builder.[ print(','), print(xyzw.w), print(']') ]
        case TYPE_ANCHOR
          write_json_string( Anchor(xy).name, builder )
        case TYPE_BOX
          builder.[ print('['), print(box.position.x), print(','), print(box.position.y) ]
          builder.[ print(','), print(box.size.x),     print(','), print(box.size.y), print(']') ]
        case TYPE_CIRCLE
          builder.[ print("[["), print(circle.position.x), print(','), print(circle.position.y) ]
          builder.[ print("],"), print(circle.radius.x), print("]") ]
        case TYPE_OBJECT
          object.write_json( builder, &=formatted, &=omit_commas )
        case TYPE_STRING
          write_json_string( object->(as String), builder )
        case TYPE_LIST
          if (count)
            builder.print '['
            if (formatted) builder.indent+=2; builder.println
            forEach (item at i in object->(as Boss::Variant[]))
              if (i)
                if (omit_commas) builder.print ' '
                else             builder.print ','
                if (formatted) builder.println
              endIf
              item.write_json( builder, &=formatted, &=omit_commas )
            endForEach
            if (formatted) builder.indent-=2; builder.println
            builder.print ']'
          else
            builder.print "[]"
          endIf
        case TYPE_TABLE
          if (count)
            builder.print '{'
            if (formatted) builder.indent+=2; builder.println
            forEach (entry at i in object->(as [Boss::Variant:Boss::Variant]).entries)
              if (i)
                if (omit_commas) builder.print ' '
                else             builder.print ','
                if (formatted) builder.println
              endIf
              write_json_string( entry.key, builder )
              builder.print ':'
              entry.value.write_json( builder, &=formatted, &=omit_commas )
            endForEach
            if (formatted) builder.indent-=2; builder.println
            builder.print '}'
          else
            builder.print "{}"
          endIf
      endWhich

    method write_json_string( st:String, builder:String )
      builder.print '"'
      write_json_character( (forEach in st), builder )
      builder.print '"'

    method write_json_character( ch:Character, builder:String )
      which (ch)
        case '"':
          builder.print( "\\\"" )
        case '\\':
          builder.print( "\\\\" )
        case '\b':
          builder.print( "\\b" )
        case '\f':
          builder.print( "\\f" )
        case '\n':
          builder.print( "\\n" )
        case '\r':
          builder.print( "\\r" )
        case '\t':
          builder.print( "\\t" )
        others
          if (ch >= 32 and ch <= 126)
            builder.print( ch )
          elseIf (ch < 32 or ch == 127 or ch == 0x2028 or ch == 0x2029)
            # RE: 2028/2029:
            # http://stackoverflow.com/questions/2965293/javascript-parse-error-on-u2028-unicode-character
            builder.print( "\\u" )
            local n = ch : Int
            loop 4
              local digit = (n :>>>: 12) & 15
              n = n:<<:4
              if (digit <= 9)
                builder.print( digit )
              else
                builder.print( ('a' + (digit - 10))->Character )
              endIf
            endLoop
          else
            # Store printable Unicode without encoding as \\uXXXX
            builder.print( ch )
          endIf
      endWhich
endClass

augment Object
  METHODS
    method write_json( builder:String, &formatted, &omit_commas )
      builder.[ print(''{"type":"''), print(type_name), print(''"}'') ]
endAugment

augment Optional
  GLOBAL METHODS
    method create_from( value:Boss::Variant )->$DataType?
      return $DataType?( value->$DataType )
endAugment

augment TableKeysIterator<<Rogue::Boss::Variant,Rogue::Boss::Variant>>
  METHODS
    method to->Boss::Variant
      return Boss::Variant(to_list)
endAugment

augment TableValuesIterator<<Rogue::Boss::Variant,Rogue::Boss::Variant>>
  METHODS
    method to->Boss::Variant
      return Boss::Variant(to_list)
endAugment

