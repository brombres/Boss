module Boss

class Procedure : ControlStructure
  PROPERTIES
    type_context   : Type
    name           : String
    parameters     : Parameters
    return_type    : Type
    attributes     : Attributes
    doc            : String
    _signature     : String
    param_count    : Int  # Includes 'this' context.
    local_count    : Int  # Count of all non-parameter locals in this scope and all nested scopes.

  METHODS
    method init( t, name, parameters, return_type, attributes, doc, statements )
      ensure<<attributes>>(t)

    method init( t, name, parameters=null, return_type=null, statements=null )
      ensure<<attributes>>(t)
      ensure<<statements>>(t)

    method init( existing:Procedure )
      prior.init( existing )
      name = existing.name
      if (existing.parameters) parameters = existing.parameters.cloned
      if (existing.return_type) return_type = existing.return_type.cloned
      if (existing.attributes) attributes = existing.attributes.cloned
      doc = existing.doc

    method description->String
      return "$($,$,$,$,$,$)" (type_name,%name,%parameters,%return_type,%attributes,%doc,%statements)

    method signature->String
      if (_signature) return _signature

      _signature = String()
      _signature.print( name )
      _signature.print( '(' )
      if (parameters)
        forEach (param at i in parameters)
          assert param.type
          if (i) _signature.print ','
          _signature.print param.type
        endForEach
      endIf
      _signature.print( ')' )
      return _signature

endClass

